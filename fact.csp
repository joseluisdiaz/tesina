
MAX_INT = 6
V = {0 .. MAX_INT}

PID = { 1, 2, 3 }

-- Small Int representation
datatype SmallInt = SI.V | Overflow
--
add(SI.a, SI.b) = let sum = a + b within if sum <= MAX_INT then SI.sum else Overflow
add(_, _) = Overflow

sub(SI.a, SI.b) = let sub = a - b within if sub >= 0 then SI.sub else Overflow
sub(_, _) = Overflow

mult(SI.a, SI.b) = let mult = a * b within if mult <= MAX_INT then SI.mult else Overflow
mult(_, _) = Overflow

eq(SI.a, SI.b) = a == b
eq(_, _) = false

-- Possible actor names
datatype ActorName = Factorial | FactorialWorker | Main

--
datatype ActorID = Id.ActorName.PID

-- Possible types for actors
datatype M = T.ActorID | Value.SmallInt | None

-- Start message
channel Create:ActorID.(M,M)

-- External ( actor to mailbox ) and Internal ( mailbox to actor ) communication
channel CommEx:ActorID.(M, M)
channel CommIn:ActorID.(M, M)

channel newPid:ActorID

mailbox(i, <>) = CommEx?i.x -> mailbox(i, <x>)
mailbox(i, <x>) = CommIn!i.x -> mailbox(i,<>) [] CommEx?i.y -> mailbox(i, <x, y>)
mailbox(i, <x,y>) = CommIn!i.x -> mailbox(i,<y>) [] CommIn!i.y -> mailbox(i, <x>)

-- Comportamiento del Actor Factorial
factorial = Create!Id.Factorial.1?(None, None) -> factorialRunning(Id.Factorial.1)
factorialRunning(self) = CommIn?self.(T.mailboxClient, Value.k) ->
if (eq(k,SI.0))
  then
    CommEx!mailboxClient.(Value.SI.1, None) -> factorialRunning(self)
  else
    let
      newK = sub(k, SI.1)
    within
      Create?Id.FactorialWorker.pid!(Value.k, T.mailboxClient) ->
      CommEx!self.(T.Id.FactorialWorker.pid, Value.newK) ->
      factorialRunning(self)

factorialRunning(_) = STOP

-- Comportamiento del actor factorialWorker
factorialWorker  = ||| actorId : {|Id.FactorialWorker|} @ Create!actorId?(Value.k, T.mailboxClient) -> factorialWorkerRunning(actorId, Value.k, T.mailboxClient)
factorialWorkerRunning(self, Value.k, T.mailboxClient) = CommIn?self.(Value.n, None) ->
    let
      val = mult(n, k)
    within
      CommEx!mailboxClient.(Value.val, None) ->
      STOP

factorialWorkerRunning(_, _, _) = STOP

actor_main =
  Create?Id.Factorial.pid!(None, None) ->
  CommEx!Id.Factorial.pid.(T.Id.Main.1, Value.SI.3) ->
  CommIn?Id.Main.1.(Value.X, None) ->
  STOP



--
COMM = {|CommEx, CommIn|}
--

mailboxes = mailbox(Id.Factorial.1, <>) ||| mailbox(Id.Main.1, <>)
CREATES1 = {| Create.Id.FactorialWorker |}
CREATES2 = {| Create.Id.Factorial |}


SYSTEM = ((factorial [|CREATES1|] factorialWorker) [|CREATES2|] actor_main) [|COMM|] mailboxes
