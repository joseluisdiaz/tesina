-- Small Int representation
MAX_INT = 6
datatype SmallInt = SI.{0 .. MAX_INT} | Overflow

add(SI.a, SI.b) =
  let sum = a + b within if sum <= MAX_INT then SI.sum else Overflow
add(_, _) = Overflow

sub(SI.a, SI.b) =
  let sub = a - b within if sub >= 0 then SI.sub else Overflow
sub(_, _) = Overflow

mult(SI.a, SI.b) =
  let mult = a * b within if mult <= MAX_INT then SI.mult else Overflow
mult(_, _) = Overflow

eq(SI.a, SI.b) = a == b
eq(_, _) = false

-- Possible actor names
datatype ActorID = queue.{1} | node.{1,2,3} | main.{1} | NoId

-- Possible Strings
datatype Atoms = ENQUEUE | DEQUEUE | INSERT | DELETE

-- Possible actor values
datatype VALUE = ACTOR.ActorID | INT.SmallInt | ATOM.Atoms | None

-- Actor creation decoupling
channel CreateAsk:ActorID.(VALUE, VALUE)
channel Create:ActorID.(VALUE, VALUE)

create(actorId) = CreateAsk!actorId?m -> Create.actorId!m -> STOP
creates = ||| actor: ActorID @ create(actor)

-- Send ( actor to mailbox ) and Recv ( mailbox to actor ) communication
channel CommSend:ActorID.(VALUE, VALUE)
channel CommRecv:ActorID.(VALUE, VALUE)
MAILBOX_SIZE = 3

buff(left, right, <>) = left?msg -> buff(left, right, <msg>)

buff(left, right, xs) = if (length(xs) < MAILBOX_SIZE - 1) then
    buff_with_space(left, right, xs)
  else
    buff_full(left, right, xs)

buff_with_space(left, right, <x> ^ xs) =
  right!x -> buff(left, right, xs)
  []
  left?y -> buff(left, right, <x> ^ xs ^ <y>)

buff_full(left, right, <x> ^ xs) = right!x -> buff(left, right, xs)

mailboxes = ||| actor: ActorID @ buff(CommSend.actor, CommRecv.actor, <>)

-- become (communication forwarding)
fwd(in, out) = CommRecv.in?msg -> CommSend.out!msg -> fwd(in, out)

-- Node actor
Nodes = ||| actorId : {|node|} @ Create.actorId?(INT.content, ACTOR.link) ->
  Node(actorId, content, link)

Node(self, content, link) =
  CommRecv.self?(ATOM.DELETE, ACTOR.client) ->
  CommSend.client!(ACTOR.link, INT.content) ->
  STOP
  []
  CommRecv.self?(ATOM.INSERT, ACTOR.newLink) ->
  Node(self, content, newLink)

-- Queue Actor
Queues = ||| actorId : {|queue|} @ Create.actorId?(None, None) -> EmptyQueue(actorId)

EmptyQueue(self) =
  CommRecv.self?(ATOM.ENQUEUE, INT.value) ->
  CreateAsk?node.pid!(INT.value, ACTOR.NoId) ->
  Queue(self, node.pid, node.pid)

Queue(self, first, last) =
  CommRecv.self?(ATOM.ENQUEUE, INT.value) ->
  CreateAsk?node.newLast!(INT.value, ACTOR.NoId) ->
  CommSend.first!(ATOM.INSERT, ACTOR.node.newLast) ->
  Queue(self, first, node.newLast)
  []
  CommRecv.self?(ATOM.DEQUEUE, ACTOR.client) ->
  CommSend.first!(ATOM.DELETE, ACTOR.self) ->
  CommRecv.self?(ACTOR.newFirst, INT.value) ->
  CommSend.client!(INT.value, None) ->
  if (newFirst == NoId) then
    EmptyQueue(self)
  else
    Queue(self, newFirst, last)

actor_main1 = CreateAsk?queue.pid!(None, None) ->
              actor_main1_r(queue.pid)

actor_main1_r(pid) =
    CommSend.pid!(ATOM.ENQUEUE, INT.SI.1) -> actor_main1_r(pid) |~|
    CommSend.pid!(ATOM.ENQUEUE, INT.SI.2) -> actor_main1_r(pid) |~|
    CommSend.pid!(ATOM.DEQUEUE, ACTOR.main.1) ->
    CommRecv.main.1?(INT.V, None) ->
    actor_main1_r(pid)

actor_main2 = CreateAsk?queue.pid!(None, None) ->
              actor_main2_r(queue.pid)

actor_main2_r(pid) =
      CommSend.pid!(ATOM.ENQUEUE, INT.SI.1) ->
      CommSend.pid!(ATOM.ENQUEUE, INT.SI.2) ->
      actor_main2_r(pid) |~|
      CommSend.pid!(ATOM.DEQUEUE, ACTOR.main.1) ->
      CommRecv.main.1?(INT.V, None) ->
      actor_main2_r(pid)

--
COMM = {|CommSend, CommRecv, Create, CreateAsk|}
--

SYSTEM1 = (Queues ||| Nodes ||| actor_main1) [|COMM|] ( mailboxes ||| creates )
SYSTEM2 = (Queues ||| Nodes ||| actor_main2) [|COMM|] ( mailboxes ||| creates )

assert SYSTEM2 [T= SYSTEM1
assert SYSTEM1 [T= SYSTEM2
