-- Small Int representation
MAX_INT = 6
datatype SmallInt = SI.{0 .. MAX_INT} | Overflow
--
add(SI.a, SI.b) = let sum = a + b within if sum <= MAX_INT then SI.sum else Overflow
add(_, _) = Overflow

sub(SI.a, SI.b) = let sub = a - b within if sub >= 0 then SI.sub else Overflow
sub(_, _) = Overflow

mult(SI.a, SI.b) = let mult = a * b within if mult <= MAX_INT then SI.mult else Overflow
mult(_, _) = Overflow

eq(SI.a, SI.b) = a == b
eq(_, _) = false


-- Possible actor names
datatype ActorID = Node.{1,2,3} | Main.{1} | NoId

datatype Atoms = PUSH | POP | F

-- Possible types for actors
datatype VALUE = ACTOR.ActorID | INT.SmallInt | ATOM.Atoms | None

-- Start message
channel CreateAsk:ActorID.(VALUE, VALUE)
channel Create:ActorID.(VALUE, VALUE)

-- Send ( actor to mailbox ) and Recv ( mailbox to actor ) communication
channel CommSend:ActorID.(VALUE, VALUE)
channel CommRecv:ActorID.(VALUE, VALUE)


buff(left, right, {}, N) = left?msg -> buff(left, right, {msg}, N)

buff(left, right, msgs, N) = [] msg: msgs @ right!msg -> buff(left, right, diff(msgs, {msg}), N)
                             [] (card(msgs) < N - 1 & left?msg -> buff(left, right, union(msgs, {msg}), N))

MAILBOX_SIZE = 3
mailboxes = ||| actor: ActorID @ buff(CommSend.actor, CommRecv.actor, {}, MAILBOX_SIZE)

create(actorId) = CreateAsk!actorId?m -> Create.actorId!m -> STOP
creates = ||| actor: ActorID @ create(actor)

-- become link
fwd(in, out) = CommRecv.in?msg -> CommSend.out!msg -> fwd(in, out)

-- Comportamiento del Actor Node
node = ||| actorId : {|Node|} @ Create.actorId?(INT.content, ACTOR.link) -> nodeRunning(actorId, content, link)

nodeRunning(self, content, link) =
  CommRecv.self?(ATOM.PUSH, INT.newContent) ->
  CreateAsk?Node.newNode!(INT.content, ACTOR.link) ->
  nodeRunning(self, newContent, Node.newNode)
  []
  CommRecv.self?(ATOM.POP, ACTOR.client) ->
  CommSend.client!(INT.content, None) ->
  fwd(self, link)
  []
  CommRecv.self?(ATOM.F, None) ->
  CommSend.link!(ATOM.F, None) ->
  nodeRunning(self, add(content, SI.1), link)


actor_main =
let
  self = Main.1
within
    CreateAsk?Node.pid!(INT.SI.3, ACTOR.NoId) ->
    CommSend.Node.pid!(ATOM.PUSH, INT.SI.2) ->
    CommSend.Node.pid!(ATOM.PUSH, INT.SI.1) ->
    CommSend.Node.pid!(ATOM.F, None) ->
    CommSend.Node.pid!(ATOM.POP, ACTOR.self) ->
    CommRecv.self?(INT.V, None) ->
    CommSend.Node.pid!(ATOM.POP, ACTOR.self) ->
    CommRecv.self?(INT.V, None) ->
    CommSend.Node.pid!(ATOM.POP, ACTOR.self) ->
    CommRecv.self?(INT.V, None) ->
    STOP

--
COMM = {|CommSend, CommRecv, Create, CreateAsk|}
--


SYSTEM = ( node ||| actor_main) [|COMM|] ( mailboxes ||| creates )
